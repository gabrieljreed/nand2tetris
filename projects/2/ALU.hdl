// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Handle zx
    Mux16(a=x, b=false, sel=zx, out=zero_x);

    // Handle nx
    Not16(in=zero_x, out=not_x);
    Mux16(a=zero_x, b=not_x, sel=nx, out=x_to_use);

    // Handle zy
    Mux16(a=y, b=false, sel=zy, out=zero_y);

    // Handle ny
    Not16(in=zero_y, out=not_y);
    Mux16(a=zero_y, b=not_y, sel=ny, out=y_to_use);

    // Calculate add or and
    Add16(a=x_to_use, b=y_to_use, out=add_result);
    And16(a=x_to_use, b=y_to_use, out=and_result);

    // Pick function
    Mux16(a=and_result, b=add_result, sel=f, out=func_result);

    // Handle no
    Not16(in=func_result, out=not_func_result);
    Mux16(a=func_result, b=not_func_result, sel=no, out=out, out[15]=sign_bit, out[0..7]=out_1, out[8..15]=out_2);

    // Set ng
    Mux(a=false, b=true, sel=sign_bit, out=ng);

    // Set zr
    Or8Way(in=out_1, out=or_1);
    Or8Way(in=out_2, out=or_2);
    Or(a=or_1, b=or_2, out=not_zr);
    Not(in=not_zr, out=zr);
}