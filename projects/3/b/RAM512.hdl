// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM512.hdl
/**
 * Memory of 512 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    // address[0..2] selects which RAM64 to use
    // address[3..8] selects which RAM8 and register within the RAM64 to use
    // For example, with the address 111101010:
    // 0..2 = 111 (7), which selects RAM64 7
    // 3..5 = 010 (2), which selects RAM8 2
    // 6..8 = 101 (5), which selects register 5 within RAM8 2
    DMux8Way(in=load, sel=address[0..2], a=load_r0, b=load_r1, c=load_r2, d=load_r3, e=load_r4, f=load_r5, g=load_r6, h=load_r7);

    RAM64(in=in, load=load_r0, address=address[3..8], out=r0);
    RAM64(in=in, load=load_r1, address=address[3..8], out=r1);
    RAM64(in=in, load=load_r2, address=address[3..8], out=r2);
    RAM64(in=in, load=load_r3, address=address[3..8], out=r3);
    RAM64(in=in, load=load_r4, address=address[3..8], out=r4);
    RAM64(in=in, load=load_r5, address=address[3..8], out=r5);
    RAM64(in=in, load=load_r6, address=address[3..8], out=r6);
    RAM64(in=in, load=load_r7, address=address[3..8], out=r7);

    Mux8Way16(a=r0, b=r1, c=r2, d=r3, e=r4, f=r5, g=r6, h=r7, sel=address[0..2], out=out);
}
